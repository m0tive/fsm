import os

Import( 'global_env' )
env = global_env.Clone()

# Defining unit test runner from http://www.scons.org/wiki/UnitTests
def builder_unit_test( target, source, env ):
    app = str( source[0].abspath )
    if os.spawnl( os.P_WAIT, app, app ) == 0:
        open( str( target[0] ), 'w' ).write( "PASSED\n" )
    else:
        return 1

bld = Builder( action = builder_unit_test )
env.Append( BUILDERS = { 'Test' : bld } )

env.AppendUnique( CPPPATH = [ '#googletest/include', '#include' ] )
env.AppendUnique( LIBS = [ 'gtest_main', 'cm2' ] )

if 'msvc' in env['TOOLS'] :
    env.AppendUnique( CCFLAGS = [ '/EHsc' ] )

test_files = [ 'cm2-Vector2.cpp']

unittest = env.Program( '#bin/test', test_files )
env.Test( "test.passed", unittest )
