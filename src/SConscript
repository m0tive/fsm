# cm2 library scons script

Import('global_env')

env = global_env.Clone()
env.AppendUnique( CPPPATH = [ '#include' ] )
env.AppendUnique( CPPDEFINES = [ env['PLATFORM'].upper() ] )
env.AppendUnique( LIBPATH = [ '#lib' ] )

if env['USE_MSC_STDINT']:
    env.AppendUnique( CPPDEFINES = [ 'CM2_USE_MSC_STDINT' ] )

if GetOption('debug') :
    env.AppendUnique( CPPDEFINES = [ '_DEBUG' ] )
    if 'g++' in env['TOOLS'] :
        env.AppendUnique( CCFLAGS = [ '-g', '-Wall', '-Wextra' ] )
    elif 'msvc' in env['TOOLS'] :
        env.AppendUnique( CCFLAGS = [ '/Od', '/Gm', '/RTC1',
            '/W3', '/Wp64', '/Zi', '/EHsc' ] )
else :
    env.AppendUnique( CPPDEFINES = [ 'NDEBUG' ] )
    if 'g++' in env['TOOLS'] :
        env.AppendUnique( CCFLAGS = [ '-O3', '-funroll-loops', '-ffast-math' ] )
    elif 'msvc' in env['TOOLS'] :
        env.AppendUnique( CCFLAGS = [ '/Ox', '/EHsc' ] )

src_files = [ 'Def.cpp',
              'Vector2.cpp',
              'Vector3.cpp' ]

env.Library( '#lib/cm2', src_files )
